(
    InputTable as table, 
    InitialColumnCount as number,
    optional AddColumnTotal as logical 
) =>

let
    Cols = 
        List.Skip(
            Table.ColumnNames(InputTable), 
            InitialColumnCount
        ),

    InitialCols = 
        List.FirstN(
            Table.ColumnNames(InputTable), 
            InitialColumnCount
        ),

    TotalLabelAdded =  
        List.Accumulate(
            InitialCols,
            [],
            (s,c)=> Record.AddField(s, c, "Total")
        ),

    TotalRecord = 
        List.Accumulate(
            Cols,
            TotalLabelAdded,
            (s,c)=> Record.AddField(s, c, List.Sum(Table.Column(InputTable, c)))    
        ),

    RowTotal = 
        Table.InsertRows(
            InputTable, 
            Table.RowCount(InputTable), {TotalRecord}
        ),
    
    ColTotal = 
        Table.AddColumn(
            RowTotal, "Col Total", 
            each List.Sum(List.Select(Record.ToList(_), each _ is number))
        )
in

    if AddColumnTotal = true then ColTotal else RowTotal